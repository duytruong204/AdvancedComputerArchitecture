# -----------------------------------------------------
# Stack initialization
# -----------------------------------------------------
li sp, 0x000007FC # max 0x000007FF

# -----------------------------------------------------
# Initialize variables
# -----------------------------------------------------
jal ra, declare_variable

# -----------------------------------------------------
# Main loop
# -----------------------------------------------------
loop:

    # Delay 10 ms
    li a0, 10
    jal ra, delay_ms

    # Display time on LEDs
    li s0, 0x00000040 
    lw a0, 0(s0)    # Minutes tens
    lw a1, 4(s0)    # Minutes ones
    lw a2, 8(s0)    # Seconds tens
    lw a3, 12(s0)   # Seconds ones
    lw a4, 16(s0)   # .Seconds tens (0-9)
    lw a5, 20(s0)   # .Seconds ones (0-9)
    jal ra, hex_led_display

    # Read switches
    li a0, 0x10010000
    jal ra, read_sw
    li s0, 0x1
    beq a0, s0, stop
    beq a1, s0, reset

    # Update watch time
    li s0, 0x00000040
    lw a0, 0(s0)           # Minutes tens
    lw a1, 4(s0)           # Minutes ones
    lw a2, 8(s0)           # Seconds tens
    lw a3, 12(s0)          # Seconds ones
    lw a4, 16(s0)          # .Seconds tens
    lw a5, 20(s0)          # .Seconds ones
    jal ra, watch_run
    sw a0, 0(s0)
    sw a1, 4(s0)
    sw a2, 8(s0)
    sw a3, 12(s0)
    sw a4, 16(s0)
    sw a5, 20(s0)

    j loop

# -----------------------------------------------------
stop:
    j loop

# -----------------------------------------------------
reset:
    jal ra, declare_variable
    j loop

# -----------------------------------------------------
# Function: declare_variable
# -----------------------------------------------------
declare_variable:
    # Allocate stack (8 bytes)
    addi sp, sp, -8
    sw t0, 0(sp)
    sw t1, 4(sp)

    # -------------------------------------------------
    # Digit table (0–F) at 0x00000000
    # Each entry = 1 byte, aligned to 4 bytes for sw
    # -------------------------------------------------
    li t0, 0x00000000       # Base address
    li t1, 0x40             # 0
    sw t1, 0(t0)
    li t1, 0x79             # 1
    sw t1, 4(t0)
    li t1, 0x24             # 2
    sw t1, 8(t0)
    li t1, 0x30             # 3
    sw t1, 12(t0)
    li t1, 0x19             # 4
    sw t1, 16(t0)
    li t1, 0x12             # 5
    sw t1, 20(t0)
    li t1, 0x02             # 6
    sw t1, 24(t0)
    li t1, 0x78             # 7
    sw t1, 28(t0)
    li t1, 0x00             # 8
    sw t1, 32(t0)
    li t1, 0x10             # 9
    sw t1, 36(t0)
    li t1, 0x08             # A
    sw t1, 40(t0)
    li t1, 0x03             # B
    sw t1, 44(t0)
    li t1, 0x46             # C
    sw t1, 48(t0)
    li t1, 0x21             # D
    sw t1, 52(t0)
    li t1, 0x06             # E
    sw t1, 56(t0)
    li t1, 0x0E             # F
    sw t1, 60(t0)

    # -------------------------------------------------
    # Time initialization (00:00.00), stored at 0x00000040
    # -------------------------------------------------
    li t0, 0x00000040       # Base address  
    li t1, 0
    sw t1, 0(t0)    # Minutes tens
    sw t1, 4(t0)    # Minutes ones
    sw t1, 8(t0)    # Seconds tens
    sw t1, 12(t0)   # Seconds ones
    sw t1, 16(t0)   # .Seconds tens
    sw t1, 20(t0)   # .Seconds ones

    # Restore registers
    lw t0, 0(sp)
    lw t1, 4(sp)
    addi sp, sp, 8
    ret

# -----------------------------------------------------
# Function: hex_led_display
# Display 6 digits on 2 LED registers
# Inputs: a0..a5 = digits 0–9 (indexes into pattern table)
# -----------------------------------------------------
hex_led_display:
    # Allocate stack (20 bytes)
    addi sp, sp, -20
    sw t0, 0(sp)
    sw t1, 4(sp)
    sw t4, 8(sp)
    sw t5, 12(sp)
    sw ra, 16(sp)

    # Load digit table base address
    li t4, 0x00000000
    li t5, 0

    # Upper LED: a0,a1
    slli t0, a0, 2
    add t0, t0, t4
    lw t1, 0(t0)
    slli t1, t1, 8
    or t5, t5, t1

    slli t0, a1, 2
    add t0, t0, t4
    lw t1, 0(t0)
    or t5, t5, t1

    li t0, 0x10003000
    sw t5, 0(t0)

    # Lower LED: a2,a3,a4,a5
    li t5, 0

    slli t0, a2, 2
    add t0, t0, t4
    lw t1, 0(t0)
    slli t1, t1, 24
    or t5, t5, t1

    slli t0, a3, 2
    add t0, t0, t4
    lw t1, 0(t0)
    slli t1, t1, 16
    or t5, t5, t1

    slli t0, a4, 2
    add t0, t0, t4
    lw t1, 0(t0)
    slli t1, t1, 8
    or t5, t5, t1

    slli t0, a5, 2
    add t0, t0, t4
    lw t1, 0(t0)
    or t5, t5, t1

    li t0, 0x10002000
    sw t5, 0(t0)

    # Restore registers
    lw t0, 0(sp)
    lw t1, 4(sp)
    lw t4, 8(sp)
    lw t5, 12(sp)
    lw ra, 16(sp)
    addi sp, sp, 20
    ret

# -----------------------------------------------------
# Function: watch_run
# Increment time by 0.01 second
# -----------------------------------------------------
watch_run:
    addi sp, sp, -12
    sw t0, 0(sp)
    sw t1, 4(sp)
    sw ra, 8(sp)

    addi a5, a5, 1
    li t0, 10
    blt a5, t0, watch_end

    li a5, 0
    addi a4, a4, 1
    blt a4, t0, watch_end

    li a4, 0
    addi a3, a3, 1
    blt a3, t0, watch_end

    li a3, 0
    addi a2, a2, 1
    li t1, 6
    blt a2, t1, watch_end

    li a2, 0
    addi a1, a1, 1
    li t0, 10
    blt a1, t0, watch_end

    li a1, 0
    addi a0, a0, 1
    li t1, 6
    blt a0, t1, watch_end

    li a0, 0

watch_end:
    lw t0, 0(sp)
    lw t1, 4(sp)
    lw ra, 8(sp)
    addi sp, sp, 12
    ret

# -----------------------------------------------------
# Function: read_sw
# Inputs: a0 = SW address
# Outputs: a0 = stop, a1 = reset
# -----------------------------------------------------
read_sw:
    addi sp, sp, -12
    sw t0, 0(sp)
    sw t1, 4(sp)
    sw ra, 8(sp)

    mv t0, a0
    lw t1, 0(t0)
    andi a0, t1, 0x1       # Stop
    srli a1, t1, 1
    andi a1, a1, 0x1        # Reset

    lw t0, 0(sp)
    lw t1, 4(sp)
    lw ra, 8(sp)
    addi sp, sp, 12
    ret

# -----------------------------------------------------
# Function: delay_ms
# -----------------------------------------------------
delay_ms:
    addi sp, sp, -16
    sw t0, 0(sp)
    sw t1, 4(sp)
    sw t2, 8(sp)
    sw ra, 12(sp)

    li t0, 0
    li t1, 25000
    li t2, 0
delay_ms_loop:
    addi t0, t0, 1
    blt t0, t1, delay_ms_loop
    li t0, 0
    addi t2, t2, 1
    blt t2, a0, delay_ms_loop

    lw t0, 0(sp)
    lw t1, 4(sp)
    lw t2, 8(sp)
    lw ra, 12(sp)
    addi sp, sp, 16
    ret

# -----------------------------------------------------
# Function: delay_us
# -----------------------------------------------------
delay_us:
    addi sp, sp, -16
    sw t0, 0(sp)
    sw t1, 4(sp)
    sw t2, 8(sp)
    sw ra, 12(sp)

    li t0, 0
    li t1, 25
    li t2, 0
delay_us_loop:
    addi t0, t0, 1
    blt t0, t1, delay_us_loop
    li t0, 0
    addi t2, t2, 1
    blt t2, a0, delay_us_loop

    lw t0, 0(sp)
    lw t1, 4(sp)
    lw t2, 8(sp)
    lw ra, 12(sp)
    addi sp, sp, 16
    ret
